
_WS: /[\r\n\t ]+/
%ignore _WS
_sepby{x, sep}: x (sep x)*
_sepby0{x, sep}: [x (sep x)*]

////////////////  Keywords and Symbols  ////////////////

_WHERE:        "where"
_MODULE:       "module"
_IMPORTING:    "importing"
_UNQUALIFIED:  "unqualified"
_AS:           "as"
_USING:        "using"
_HIDING:       "hiding"
_RENAMING:     "renaming"
_TO:           "to"
_DATA:         "data"
_TYPE:         "type"
_CLASS:        "class"
_INSTANCE:     "instance"
_CASE:         "case"
_OF:           "of"
_LET:          "let"
_IN:           "in"
_FORALL:       "forall"
_TYPEKIND:     "Type"
INFIX:         "infix"
INFIXL:        "infixl"
INFIXR:        "infixr"

_EQ:         "="
_BIG_ARROW:  "=>"
_COLON:      ":"
_ARROW:      "->"
_LAMBDA:     "\\"
_AT:         "@"
_DOT:        "."
_COMMA:      ","
_PIPE:       "|"
_DOUBLEPIPE: "||"
_LPAR:       "("
_RPAR:       ")"
_LBRACE:     "{"
_RBRACE:     "}"
_LBRACK:     "["
_RBRACK:     "]"
_DOUBLESEMI: ";;"
_UNDER:      "_"

_COMMENT:    "--" /[^\n]*/ "\n"
%ignore _COMMENT






////////////////  Names and Operators  ////////////////

VARNAME: /(?!(_|where|module|import|unqualified|as|using|hiding|to|data|type|class|instance|case|of|let|in|forall)\b)[a-z_][a-zA-Z0-9_]*/
CONNAME: /(?!(Type)\b)[A-Z][a-zA-Z0-9_]*/
//OPERATOR: /(?!(=|=>|:|->|\\|@|\.|,|\||;;)\b)[~!@#$%^&*\\\-|+=:;<>?\/\.]+/
OPERATOR: /(=>|:|->|@|,|\||;;)[~!@#$%^&*\-|+=:;<>?\/\.]+/
        | /=[~!@#$%^&*\-|+=:;<?\/\.][~!@#$%^&*\-|+=:;<>?\/\.]*/
        | /-([~!@#$%^&*\|+=:;<?\/\.][~!@#$%^&*\-|+=:;<>?\/\.]*)?/
        | /;([~!@#$%^&*\-|+=:<>?\/\.][~!@#$%^&*\-|+=:;<>?\/\.]*)?/
        | /[~!#$%^&*+<>?\/\.][~!@#$%^&*\-|+=:;<>?\/\.]*/
_name: VARNAME | CONNAME | _LPAR OPERATOR _RPAR
_varname: VARNAME | _LPAR OPERATOR _RPAR
tyvarnames: VARNAME+
dotted_module_name: _sepby{CONNAME, _DOT}





////////////////  Modules  ////////////////


module: _MODULE imports _WHERE declarations _DOUBLESEMI?

imports: [_IMPORTING import+]

import: _PIPE _import_sort renaming

_import_sort: qualified_import
            | unqualified_import
            | as_import

qualified_import: dotted_module_name
unqualified_import: dotted_module_name _UNQUALIFIED using_hiding
as_import: dotted_module_name _AS CONNAME

using_hiding: (import_using | import_hiding)?

import_using: _USING _LPAR _sepby{_name, _COMMA} _RPAR
import_hiding: _HIDING _LPAR _sepby{_name, _COMMA} _RPAR

renaming: (_RENAMING _LPAR _renaming_clause (_COMMA _renaming_clause)* _RPAR)?

_renaming_clause: varname_renaming
                | conname_renaming

varname_renaming: _varname _TO _varname
conname_renaming: CONNAME _TO CONNAME






////////////////  Declarations  ////////////////

declarations: _declaration*

// Declaration Decl
//   ::=  data Cn TVK* where ConDecl*;;             (Data Declaration)
//     |  type f TVK* = A;;                         (Type Synonym Declaration)
//     |  class CCn TVK* <= (CC*) where MetDecl*;;  (Type Class Declaration)
//     |  x : A;;                                   (Term Type Signature)
//     |  x Pat* | M? = N;;                         (Term Equation)
//     |  instance CCn A+ <= (CC*) where MetEq*;;   (Instance Declaration)

_declaration: fixity_declaration
            | data_declaration
            | type_synonym_declaration
            | type_class_declaration
            | type_signature
            | term_equation
            | instance_declaration

tyvarkindings: tyvarkinding*
mandatory_tyvarkindings: tyvarkinding+
tyvarkinding: _LPAR tyvarnames _COLON _kind _RPAR






////////////////  Fixity Declarations  ////////////////

fixity_declaration: _fixity _fixity_level operators

_fixity: INFIX
       | INFIXL
       | INFIXR

_fixity_level: /0|[1-9][0-9]*/

operators: OPERATOR+

////////////////  Data Declarations  ////////////////

data_declaration: _DATA CONNAME tyvarkindings _WHERE data_body _DOUBLESEMI


// ConstructorDecl ConDecl ::=  Cn TVK* VT* : A  (Constructor Declaration)

data_body: constructor_declaration*

constructor_declaration: _PIPE CONNAME constructor_type_arguments constructor_arguments _COLON _type

constructor_type_arguments: constructor_type_argument*
constructor_type_argument: _LBRACE tyvarnames _COLON _kind _RBRACE

constructor_arguments: constructor_argument*
constructor_argument: _LPAR constructor_varnames _COLON _type _RPAR
constructor_varnames: VARNAME+






//////////////// Type Synonym Declarations  ////////////////

type_synonym_declaration: _TYPE VARNAME tyvarkindings _EQ _type _DOUBLESEMI






//////////////// Type Class Declarations  ////////////////

type_class_declaration: _CLASS class_quantification optional_class_precondition CONNAME class_params _WHERE class_body _DOUBLESEMI

class_quantification: _FORALL mandatory_tyvarkindings _DOT

optional_class_precondition: (_LPAR _sepby{class_constraint,_COMMA} _RPAR _BIG_ARROW)?

class_params: VARNAME*

class_body: method_declaration*

// MethodDeclaration MetDecl ::=  x : A  (Method Declaration)
method_declaration: _PIPE VARNAME _COLON _type





////////////////  Type Signature  ////////////////

type_signature: VARNAME _COLON _type _DOUBLESEMI






////////////////  Term Equation  ////////////////

term_equation: VARNAME argument_patterns guard _EQ _term _DOUBLESEMI

guard: [_DOUBLEPIPE _term]






////////////////  Instance Declaration  ////////////////

instance_declaration: _INSTANCE optional_class_quantification optional_class_precondition CONNAME instance_params _WHERE instance_body _DOUBLESEMI

optional_class_quantification: class_quantification?
instance_params: _type*
instance_body: method_equation*

// MethodEquation MetEq ::=  x Pat* = N  (Method Equation)
method_equation: _PIPE VARNAME argument_patterns guard _EQ _term






////////////////  Kinds  ////////////////

_kind: type_kind
     | arrow_kind
     | paren_kind

type_kind: _TYPEKIND

arrow_kind: _arrow_kind_left _ARROW _kind
_arrow_kind_left: type_kind
                | paren_kind

paren_kind: _LPAR _kind _RPAR






////////////////  Types  ////////////////

// Type A, B ::=  a              (Type Variable)
//             |  Cn             (Type Name)
//             |  A -> B         (Function Type)
//             |  CC => A        (Constrained Type)
//             |  forall TVK. B  (Forall Type)
//             |  A B            (Type Application)

_type: type_variable
     | type_name
     | function_type
     | constrained_type
     | forall_type
     | type_application
     | paren_type

type_variable: VARNAME

type_name: CONNAME

function_type: _function_type_left _ARROW _type
_function_type_left: type_variable
                   | type_name
                   | type_application
                   | paren_type

constrained_type: _LPAR _sepby{class_constraint,_COMMA} _RPAR _BIG_ARROW _type

forall_type: _FORALL mandatory_tyvarkindings _DOT _type

type_application: _type_application_left _type_application_right
_type_application_left: type_variable
                      | type_name
                      | type_application
                      | paren_type
_type_application_right: type_variable
                       | type_name
                       | paren_type

paren_type: _LPAR _type _RPAR






//////////////// Class Constraints ////////////////

class_constraint: CONNAME constraint_args

constraint_args: _type*






////////////////  Patterns  ////////////////

_pattern: variable_pattern
        | wildcard_pattern
        | constructor_pattern
        | paren_pattern

variable_pattern: VARNAME

wildcard_pattern: _UNDER

constructor_pattern: CONNAME argument_patterns

paren_pattern: _LPAR _pattern _RPAR

argument_patterns: _argument_pattern*

_argument_pattern: variable_pattern
                 | type_variable_pattern
                 | no_arg_constructor_pattern
                 | paren_pattern

no_arg_constructor_pattern: CONNAME

type_variable_pattern: _LBRACE VARNAME _RBRACE






////////////////  Terms  ////////////////

// Term M, N ::=  M : A               (Type Annotation)
//             |  x                   (Variable)
//             |  Cn                  (Constructor)
//             |  \x -> M             (Lambda Abstraction)
//             |  M N                 (Function Application)
//             |  \{a} -> M           (Type Abstraction)
//             |  M {A}               (Type Instantiation)
//             |  case M* of Cls*     (Case)
//             |  let LDecl+ in M     (Let)
//             |  M op N              (Infix Expression)
//             |  (op)                (Infix Operator)

_term: type_annotation
     | variable
     | constructor
     | lambda
     | application
     | case
     | let
     | infix
     | infix_operator
     | left_section
     | right_section
     | paren_term

type_annotation: _annotation_left _COLON _type
_annotation_left: variable
                | constructor
                | application
                | infix_operator
                | left_section
                | right_section
                | paren_term

variable: VARNAME

constructor: CONNAME

lambda: _LAMBDA formal_parameters _ARROW _term

formal_parameters: _formal_parameter+

_formal_parameter: untyped_variable_parameter
                 | typed_variable_parameter
                 | unkinded_type_variable_parameter
                 | kinded_type_variable_parameter

untyped_variable_parameter: _var_parameter
_var_parameter: named_var_parameter
              | wildcard_var_parameter
named_var_parameter: VARNAME
wildcard_var_parameter: _UNDER

var_parameters: _var_parameter+

typed_variable_parameter: _LPAR var_parameters _COLON _type _RPAR

unkinded_type_variable_parameter: _LBRACE _tyvar_parameter _RBRACE
_tyvar_parameter: named_tyvar_parameter
                | wildcard_tyvar_parameter
named_tyvar_parameter: VARNAME
wildcard_tyvar_parameter: _UNDER
tyvar_parameters: _tyvar_parameter+

kinded_type_variable_parameter: _LBRACE tyvar_parameters _COLON _kind _RBRACE

application: _application_left _application_right
_application_left: variable
                 | constructor
                 | application
                 | infix_operator
                 | left_section
                 | right_section
                 | paren_term
_application_right: variable
                  | constructor
                  | infix_operator
                  | left_section
                  | right_section
                  | paren_term
                  | type_instantiation

type_instantiation: _LBRACE _type _RBRACE

case: _CASE case_scrutinees _OF case_body
case_scrutinees: _case_scrutinee (_PIPE _case_scrutinee)*
_case_scrutinee: variable
               | constructor
               | application
               | infix
               | infix_operator
               | left_section
               | right_section
               | paren_term

case_body: case_clause*

// CaseClause Cls ::=  | Pat* || G -> N  (Case Clause)
case_clause: _PIPE patterns guard _ARROW _term

patterns: _pattern (_PIPE _pattern)*

let: _LET let_declarations _IN _term

let_declarations: _let_declaration (_PIPE _let_declaration)*

// LetDec LDecl ::=  x : A             (Let Type Signature)
//                |  x Pat* || M = N   (Let Term Equation)
_let_declaration: let_type_signature
                | let_term_equation

let_type_signature: VARNAME _COLON _type
let_term_equation: VARNAME argument_patterns guard _EQ _term

infix: _infix_argument_left OPERATOR _infix_argument_right
_infix_argument_left: variable
                    | constructor
                    | application
                    | infix
                    | infix_operator
                    | left_section
                    | right_section
                    | paren_term
_infix_argument_right: variable
                     | constructor
                     | application
                     | infix_operator
                     | left_section
                     | right_section
                     | paren_term

infix_operator: _LPAR OPERATOR _RPAR

left_section: _LPAR _infix_argument_right OPERATOR _RPAR

right_section: _LPAR OPERATOR _infix_argument_right _RPAR

paren_term: _LPAR _term _RPAR
